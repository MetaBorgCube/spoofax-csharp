module csharp

imports
  
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  libstratego-parallel
  include/CSharp
  lib/editor-common.generated
  lib/index-library.generated
  lib/analysis/-
  lib/nbl/-
  lib/index/partition
  new-names
  check
  desugar
  types

rules // Analysis
  
	// Main entry point for analyzes, called when a single file is opened in the editor.
  editor-analyze:
    (ast, path, project-path) -> (ast', errors, warnings, notes)
    with
      editor-init;
      (ast', entries, tasks, dependent) := <desugar-all; nabl-analyze(|<language>, path, project-path)> ast;
      (errors, warnings, notes)         := <analyze-markers> ast';
      <map(vdebug(!"Entry: "))> entries;
      <map(vdebug(!"Tasks: "))> tasks;
      <map(vdebug(!"Dependent: "))> dependent;
      <queue-analyze-multiple> <map(index-partition-file)> dependent

  editor-analyze:
    path* -> None()
    where
      not(is-tuple)
    with
      map(\p -> $[[<project-path>]/[p]]\);
      queue-analyze-multiple
      
  editor-save:
  	(_, _, _, path, project-path) -> None()
  	with
  		index-setup(|<language>, [project-path], $[[project-path]/[path]]);
  		index-commit

  analyze-markers:
    ast -> (errors, warnings, notes)
    with
      index-transaction(
        errors   := <collect-all(constraint-error, conc)> ast;
        warnings := <collect-all(constraint-warning, conc)> ast;
        notes    := <collect-all(constraint-note, conc)> ast
      )

  update-markers:
    Result(partition, initial-ast, analyzed-ast, old-entry*, new-entry*, old-task*, new-task*, _, _, _) -> 
    Result(partition, initial-ast, analyzed-ast, old-entry*, new-entry*, old-task*, new-task*, errors, warnings, notes)
    where
      not(() := initial-ast)
    with
      (errors, warnings, notes) := <analyze-markers> analyzed-ast;
      <set-markers(|initial-ast)> (analyzed-ast, errors, warnings, notes)

  queue-analyze-multiple:
    path* -> None()
    with
      if not([] := path*) then
        queue-strategy(|"analyze-multiple", "Analyzing files")
      end

  analyze-multiple:
    path* -> (task*, result'*)
    with
      nabl-analyze-multiple-work-units; set-total-work-units;
      measure-time(
        (task*, result*) := <nabl-analyze-multiple(parse-file; desugar-all, try, complete-work-unit|<language>, <project-path>)> path*;
        result'*         := <filter(update-markers)> result*
      , vdebug(!"Analysis seconds: ")
      );
      where(index-commit)
      // TODO: update markers for partitions that have had a task executed.
      
  measure-time(s, log) =
    where(before := <times>);
    s;
    where(after := <times>; !(after, before); diff-times; times-to-seconds; Fst; log)
      
rules // Editor services

  editor-resolve:
    (node, position, ast, path, project-path) -> definition
    where
    	index-setup(|<language>, [project-path], $[[project-path]/[path]]);
    	<nabl-has-reference> node;
    	definition := <nabl-resolve-collect-one> node

  editor-hover:
    (target, position, ast, path, project-path) -> $[Type: [<print-type> type]]
    where
      type := <type-of; print-type> target

  editor-complete:
    (node, position, ast, path, project-path) -> proposals
    where
     proposals := <nabl-propose-completions> ast

rules // Debugging
  
  // Prints the abstract syntax ATerm of a selection.
  debug-generate-aterm:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"aterm")> path;
      result   := <desugar-all> selected;
      <queue-analysis> $[[project-path]/[path]]
      
  // Prints the analyzed annotated abstract syntax ATerm of a selection.
  debug-generate-analyzed:
    (selected, position, ast, path, project-path) -> (filename, selected)
    with
      filename := <guarantee-extension(|"analyzed.aterm")> path
   
rules // NBL debugging
     
  debug-show-results =   
		analysis-debug-show-results(|<language>)

  debug-show-collected =     
  	analysis-debug-show-collected(|<language>)

  debug-show-tasks =     
  	analysis-debug-show-tasks(|<language>)

  debug-show-all-partitions =
    analysis-debug-show-all-partitions(|<language>)

  debug-clear-current =
    analysis-debug-clear-current(|<language>)
    
  debug-clear-all =
    analysis-debug-clear-all(|<language>)  

  debug-reload = 
  	analysis-debug-reload(|<language>)
